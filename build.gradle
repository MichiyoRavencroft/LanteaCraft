buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	}
}


apply plugin: 'forge'

if (System.getenv().BUILD_NUMBER != null)
	ext.buildNumber = System.getenv().BUILD_NUMBER?.toInteger()
else
	ext.buildNumber = 0

version = "1.0.0"
group = "pcl.LanteaCraft"
archivesBaseName = "LanteaCraft"

minecraft {
	version = "1.7.10-10.13.0.1180"
	replace "@VERSION@", project.version
	replace "@BUILD@", project.ext.buildNumber
}

sourceSets.main.java.srcDir("./src/externs/java")

jar.doFirst {
	for (String s : new File("./src/externs/java").list()) {
		File junkfile = new File(sourceSets.main.output.classesDir, s);
		logger.lifecycle(String.format("Deleting compiled extern file %s.", junkfile.getAbsolutePath()));
		delete junkfile;
	}
}

jar.version = "${minecraft.version}"
jar.classifier = "${ext.buildNumber}"
jar.manifest = manifest { attributes(
	"FMLCorePlugin": "pcl.lc.coremod.LCCoreMod",
	"FMLCorePluginContainsFMLMod": "icanhazfmlmodpweeze"
) }
apply plugin: 'curseforge'
curse {
	apiKey = project.curseforge_key
	projectId = "77342"
	releaseType = "beta"
	changelog = file("changelog.txt").text
//addGameVersion "1.7.2"
}
processResources {
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version + '-' + project.ext.buildNumber, 'mcversion':project.minecraft.version
	}
	
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}
